#!/usr/bin/perl
use warnings;
use strict;
use feature ':5.10';

=pod

=head1 NAME

github_approve_pr - set status for pull request from the command line


=head1 SYNOPSIS

    $ export GITHUB_TOKEN=TOKEN
    $ export GITHUB_HOST=github.example.com
    $ github_approve_pr my_org/my_repo 1


=head1 DESCRIPTION

This script simply sets a status (success, failure, pending) on a given pull request.  Given an org/repo and pull request number, it will show you the commits and ask you to approve it.  Yes=success, No=failure, Skip=pending.

This script is not a complete tool; it's more of a proof-of-concept.


=head1 REQUIREMENTS

=over 4

=item * Create personal access token on GitHub with necessary scopes for given endpoints (L<https://github.example.com/settings/tokens>).

You can save the token in the environment variable C<$GITHUB_TOKEN>, or pass it in the command line with C<-t>.

=back


=head1 OPTIONS

=over 4

=item B<-h|--help>

Prints brief help message and exits.

==item B<-V|--version>

Prints version and exits.

item B<-t|--token>

Personal access token to use GitHub API.  Can use environment variable C<$GITHUB_TOKEN> instead.

=item B<-H|--host>

Hostname of your GitHub Enterprise host e.g. github.example.com.  Can use environment variable C<$GITHUB_HOST> instead.

=item B<-u|--target_url>

Set a URL for the status, so a user can go to it for more information.

=item B<-u|--context>

The context to set the status on.  Default is C<reviewed>.

=back


=head1 COPYRIGHT AND LICENSE

Copyright 2016, Marchex.

This library is free software; you may redistribute it and/or modify it under the same terms as Perl itself.

=cut


BEGIN {
    use FindBin '$Bin';
    use lib "$Bin/../lib/perl";
}

use Pod::Usage;
use HTTP::Response;
use JSON::XS qw(decode_json encode_json);
use MIME::Base64;
use URI::Escape;

use Marchex::Client::GitHub;
use Marchex::Color qw(color_str :colors);

my $gh = init();

my $last_sha;
my $pr = "$gh->{repo}/pulls/$gh->{pr_num}";
my $commits = $gh->{api}->command(GET => "/repos/$pr/commits");

binmode STDOUT, ':encoding(UTF-8)';

for my $commit (@$commits) {
    print color_str("commit $commit->{sha}", YELLOW), "\n";
    print "Author: $commit->{commit}{committer}{name} <$commit->{commit}{committer}{email}>\n";
    print "Date: $commit->{commit}{author}{date}\n";
    my $message = $commit->{commit}{message};
    $message =~ s/^/    /;
    $message =~ s/\n/\n    /g;
    $message =~ s/\s+$//g;
    print "\n$message\n\n";

    $last_sha = $commit->{sha};
}


local $|;
print color_str("Approve pull request", BLUE), ' ',
      color_str($pr, RED), color_str('?', BLUE), ' [Yes/No/Skip] ';
chomp(my $ans = <>);
if ($ans =~ /^[yY]/) {
    print "Approved!\n\n";
    status($last_sha, 'success');
}
elsif ($ans =~ /^[nN]/) {
    print "Denied!\n\n";
    status($last_sha, 'failure');
}
else {
    status($last_sha, 'pending');
}

exit;

sub status {
    my($sha, $state) = @_;
    my %args = (
        state       => $state || 'error',
        description => "Reviewed-by: $gh->{user}",
        context     => $gh->{context}
    );
    $args{target_url} = $gh->{target_url} if $gh->{target_url};
    $gh->{api}->command(POST => "/repos/$gh->{repo}/statuses/$sha", \%args);
}

sub init {
    my $gh = Marchex::Client::GitHub->init(
        'u|target_url=s'        => 'target_url',
        'c|context=s'           => 'context',
    );

    (@{$gh}{qw(repo pr_num user)}) = @ARGV;
    $gh->{user}         //= $ENV{USER};
    $gh->{context}      //= 'reviewed';
    @ARGV = ();

    return($gh);
}
